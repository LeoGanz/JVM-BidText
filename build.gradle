apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'edu.purdue.cs.toydroid.bidtext.TextLeak'

sourceCompatibility = 1.8
targetCompatibility = 1.8

distTar.enabled = false

repositories {
    mavenCentral()
}

distributions {
    main {
        distributionBaseName = 'BidText'
        // include the configuration files into the distribution zip
        contents {
            into('/' + distributions.main.distributionBaseName) {
                from '.'
                include 'dat/*'
                exclude 'dat/Config.properties'
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = ["src"]
        }
        resources {
            srcDirs = ["res"]
        }
    }
}


jar {
    archiveBaseName = 'ToyDroid.BidText'
    manifest {
        attributes 'Main-Class': 'edu.purdue.cs.toydroid.bidtext.TextLeak'
    }
}

dependencies {
    implementation 'com.ibm.wala:com.ibm.wala.core:1.5.9'
    implementation 'com.ibm.wala:com.ibm.wala.cast:1.5.9'
    implementation 'com.ibm.wala:com.ibm.wala.dalvik:1.5.9'
    implementation 'org.apache.logging.log4j:log4j-core:2.22.1'

    // local libs
    implementation fileTree(dir: 'lib', include: ['*.jar'])
}


